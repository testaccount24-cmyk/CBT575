/* REXX : ASCEBC     Reformat ASCII text data                      */
/* This edit macro will read the edit file and change the data     */
/* from ASCII to EBCDIC format.  Any CR/LF are changed to line     */
/* breaks.                                                         */
/* ASCII and EBCDIC tables were taken from ICONV utility           */
/*        iconv -t IBM-1047 -f ISO88059-1 <ASCin >EBCout           */
/*******************************************************************/
/*                                                                 */
 ASCII_table  = '20A22E3C282B7C2621242A293B5E2D2F'x||,
                'A62C255F3E3F603A2340273D22616263'x||,
                '646566676869BBF0FDFEB16A6B6C6D6E'x||,
                '6F707172BAE6B8C6A4B57E7374757677'x||,
                '78797ABFD05BDEAEACA3A5B7A9A7B6BC'x||,
                'BDBEA8AF5DB4D77B4142434445464748'x||,
                '497D4A4B4C4D4E4F5051525CF7535455'x||,
                '565758595A30313233343536373839'x
 EBCDIC_table = '404A4B4C4D4E4F505A5B5C5D5E5F6061'x||,
                '6A6B6C6D6E6F797A7B7C7D7E7F818283'x||,
                '8485868788898B8C8D8E8F9192939495'x||,
                '969798999B9C9D9E9FA0A1A2A3A4A5A6'x||,
                'A7A8A9ABACADAEAFB0B1B2B3B4B5B6B7'x||,
                'B8B9BBBCBDBEBFC0C1C2C3C4C5C6C7C8'x||,
                'C9D0D1D2D3D4D5D6D7D8D9E0E1E2E3E4'x||,
                'E5E6E7E8E9F0F1F2F3F4F5F6F7F8F9'X
/*                                                                   */
ADDRESS ISREDIT "MACRO"
ADDRESS ISREDIT "RESET"                      /* remove message lines */
ADDRESS ISREDIT "(MaxLine) = LINENUM .ZLAST" /* maximum line count   */
ADDRESS ISREDIT "(Caprof) = CAPS"         /* CAPS setting            */
ADDRESS ISREDIT "CAPS OFF"                /* Set for mixed case      */
/*                                                                   */
/*                     Process the Edit Lines                        */
/*                                                                   */
do I = 1 to MaxLine
   ADDRESS ISREDIT "(LINE) = LINE" I      /* get line from editor    */
   LINE = strip(LINE,'T')                 /* strip trailing blanks   */
   SRCO = translate(LINE, EBCDIC_table, ASCII_table) /* translation  */
   SRCO = translate(SRCO, '01'x, '7D'x)   /* chg single quotes       */
   SRC = translate(SRCO, '02'x, '50'x)    /* chg ampersand           */
   ADDRESS ISREDIT "LINE_AFTER .ZLAST = '"SRC"'" /* add as last line */
   Retc = 0                               /* initialize return code  */
   do while Retc =  0                     /* while CR/LFs found      */
     ADDRESS ISREDIT "FIND '0d0a'x .ZLAST .ZLAST" /* find CR/LF      */
     Retc = RC                            /* save return code        */
     if RC = 0 then do                    /* if CR/LF found          */
       ADDRESS ISREDIT "(fline, fcol) = CURSOR"  /* get col address  */
       ADDRESS ISREDIT "TSPLIT .ZLAST " fcol  /* split line before   */
       ADDRESS ISREDIT "TSPLIT .ZLAST  3" /* split line after CR/LF  */
     end
   end
end
/*                                                                   */
ADDRESS ISREDIT "DELETE 1 " MaxLine       /* delete orig lines       */
ADDRESS ISREDIT "CHANGE '01'x '7D'x ALL"  /* chg single quotes back  */
ADDRESS ISREDIT "CHANGE '02'x '50'x ALL"  /* chg ampersand back      */
ADDRESS ISREDIT "X ALL"                   /* exclude all lines       */
ADDRESS ISREDIT "FIND '0d0a'x ALL"        /* find all CR/LF          */
ADDRESS ISREDIT "DELETE NX ALL"           /* remove all found lines  */
if CaProf = "ON" then do                  /* test for upper case     */
  ADDRESS ISREDIT "CAPS ON"               /* set to upper case       */
end
ADDRESS ISREDIT "RESET"                   /* show all chgd lines     */
/*                                                                   */
RETURN
